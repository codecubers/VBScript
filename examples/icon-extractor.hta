<html>
<head>
    <hta:application
        applicationName="Extract icons from .dll or .exe files"
        icon="msctfui.dll"
        innerBorder="No"
        scroll="No" />
    <script language="VBScript">
        Self.ResizeTo 600, 400
    </script>
</head>
<body>
    <script language="VBScript">
        Option Explicit
        'project objects
        Dim extractor 'VBScripting.IconExtractor object
        Dim format 'VBScripting.StringFormatter object
        Dim rf 'RegExFunctions object
        Dim includer 'VBScripting.Includer object
        Dim browser 'VBScripting.FolderChooser object
        Dim log 'VBSLogger object
        Dim admin 'VBScripting.Admin object
        'Windows native objects
        Dim sh 'WScript.Shell object
        Dim fso 'Scripting.FileSystemObject
        Dim regex 'RegExp object
        Dim fileDict 'Scripting.Dictionary object for storing filespecs
        Dim application 'HTML application element (HTA application)
        'other
        Dim timeoutId 'saved integer for stopping a setTimeout method
        Dim fileKeys 'array of filespecs
        Dim iconIndex 'index of an icon within a .dll or .exe file
        Dim fileIndex 'array index for fileKeys
        Dim targetFolder 'string
        Dim ms 'integer: milliseconds; for setTimeout 
        Dim iconsExtracted 'integer: number of icons extracted
        Dim CheckingMsg 'string: partial progress message
        Dim eStop 'boolean: flag for stopping the extration
        Dim iconCount 'integer: the supposed number of icons in a file
        Dim configFile 'string: filespec of configuration file
        Const F1 = 112 'window.event.KeyCode
        Const F6 = 117
        Const F7 = 118
        Const F8 = 119
        Const F9 = 120
        Const Esc = 27
        Const Enter = 13
        Const ForWriting = 2, CreateNew = True 'for OpenTextFile
        Const ListingFiles = "Listing filles"
        Const ExtractingFiles = "Extracting files"
        Const ShowingResults = "Showing results"
        Const WordPad = "%ProgramFiles%\Windows NT\Accessories\wordpad.exe"

        Sub Window_OnLoad
            InitializeObjects

            'populate fields from the config file,
            'and/or create the config file
            Dim sourceFileValue
            Dim sourceDirValue
            Dim targetDirValue
            configFile = format(Array( _
                "%s\VBScripting\%s.configure", _
                Expand("%AppData%"), _
                fso.GetBaseName(document.location.href)))
            On Error Resume Next
                Execute fso.OpenTextFile(configFile).ReadAll
                If Err Then
                    Dim configStream : Set configStream = fso.OpenTextFile(configFile, ForWriting, CreateNew)
                    configStream.WriteLine "sourceDirValue = ""%SystemRoot%\System32"""
                    configStream.WriteLine "sourceFileValue = ""*.dll | *.exe"""
                    configStream.WriteLine "targetDirValue = format(Array(""%UserProfile%\Desktop\extracted-icons\%s-%s-%s-%s-%s"", Month(Now), Day(Now), Hour(Now), Minute(Now), Second(Now)))"
                    configStream.Close
                End If
            Err.Clear
                Execute fso.OpenTextFile(configFile).ReadAll
                If Err Then
                    Feedback format(Array( _
                        "Error reading the config file. <br />" & _
                        "File: %s <br />" & _
                        "Error description: %s", _
                        configFile, Err.Description))
                End If
            On Error Goto 0

            If admin.PrivilegesAreElevated Then Disable elevateBtn
            sourceDirTxtBox.value = sourceDirValue
            sourceFileTxtBox.value = sourceFileValue
            targetDirTxtBox.value = targetDirValue
            CheckIconChoices
            CheckingMsg = "Checking for icons in<br />"
            Disable stopBtn
            ms = 0 'milliseconds; for setTimeout
        End Sub

        Sub InitializeObjects
            Set sh = CreateObject( "WScript.Shell" )
            Set fso = CreateObject( "Scripting.FileSystemObject" )
            Set format = CreateObject( "VBScripting.StringFormatter" )
            Set extractor = CreateObject( "VBScripting.IconExtractor" )
            Set application = document.getElementsByTagName( "application" )(0)
            document.Title = application.applicationName
            Set regex = New RegExp
            regex.IgnoreCase = True
            Set includer = CreateObject( "VBScripting.Includer" )
            Execute includer.Read( "RegExFunctions" )
            Set rf = New RegExFunctions
            Execute includer.Read( "VBSLogger" )
            Set log = New VBSLogger
            Set browser = CreateObject( "VBScripting.FolderChooser" )
            Set admin = CreateObject( "VBScripting.Admin" )
            Set fileDict = CreateObject( "Scripting.Dictionary" )
        End Sub

        Sub extractIconsBtn_OnClick
            InProgressMsg "Getting the file list..."
            window.setTimeout "PrepStatesFor ListingFiles", ms, "VBScript"
        End Sub

        Function PrepStatesFor(status)
            Select Case status

            Case ListingFiles
            ClearFeedback
            Disable extractIconsBtn
            inProgressDiv.style.display = "block"
            fileDict.RemoveAll
            iconsExtracted = 0
            eStop = False
            Disable subfoldersChkBox
            Disable sourceDirTxtBox
            Disable sourceBrowserBtn
            Disable sourceFileTxtBox
            Disable targetDirTxtBox
            Disable targetBrowserBtn
            Disable largeIconsChkBox
            Disable smallIconsChkBox
            Disable zeroIndexesOnlyChkBox
            Disable stopBtn
            Disable elevateBtn
            window.setTimeout "ListFiles", ms, "VBScript"

            Case ExtractingFiles
            fileKeys = fileDict.Keys
            If -1 = UBound(fileKeys) Then
                Feedback "No files found"
                PrepStatesFor = False
                PrepStatesFor ShowingResults
                Exit Function
            End If
            iconIndex = 0
            fileIndex = 0
            InProgressMsg CheckingMsg & fileKeys(0)
            targetFolder = Expand(targetDirTxtBox.value)
            Enable stopBtn
            stopBtn.focus
            Disable extractIconsBtn

            Case ShowingResults
            inProgressDiv.innerHTML = ""
            inProgressDiv.style.display = "none"
            Enable extractIconsBtn
            Enable stopBtn
            Enable subfoldersChkBox
            Enable sourceDirTxtBox
            Enable sourceBrowserBtn
            Enable sourceFileTxtBox
            Enable targetDirTxtBox
            Enable targetBrowserBtn
            Enable largeIconsChkBox
            Enable smallIconsChkBox
            Enable zeroIndexesOnlyChkBox
            Disable stopBtn
            Enable elevateBtn
            End Select

            PrepStatesFor = True
        End Function
        Sub Disable(element) : element.disabled = True : End Sub
        Sub Enable(element) : element.disabled = False : End Sub

        'Compile a list of the files to extract; then start extracting
        Sub ListFiles
            Dim dir : dir = Expand(targetDirTxtBox.value)
            If largeIconsChkBox.checked Then
                CreateFolder format(Array("%s\lg", dir))
            End If
            If smallIconsChkBox.checked Then
                CreateFolder format(Array("%s\sm", dir))
            End If
            If subfoldersChkBox.checked Then
                ListFilesBySubfolder sourceDirTxtBox.value
            ElseIf InStr( sourceFileTxtBox.value, "*" ) _
            Or InStr( sourceFileTxtBox.value, "?" ) _
            Or InStr( sourceFileTxtBox.value, "|" ) Then
                ListFilesByWildcard sourceDirTxtBox.value
            Else
                ListFile sourceDirTxtBox.value, sourceFileTxtBox.value
            End If
            If Not PrepStatesFor(ExtractingFiles) Then
                Exit Sub
            End If
            window.setTimeout "ExtractTheNextIcon", ms, "VBScript"
        End Sub

        Sub ShowResults
            PrepStatesFor ShowingResults
            Feedback "Icons extracted: " & iconsExtracted
            Feedback "Files processed: " & fileIndex
            window.setTimeout "ClearEStop", ms + 200, "VBScript"
        End Sub
        Sub ClearEStop : eStop = False : End Sub

        Sub ListFilesBySubfolder(folder)
            Dim subfolder
            For Each subfolder In fso.GetFolder(Expand(folder)).SubFolders
                ListFilesBySubfolder subfolder 'recurse
            Next
            ListFilesByWildcard folder
        End Sub

        Sub ListFilesByWildcard(sourceDir)
            regex.Pattern = rf.Pattern(sourceFileTxtBox.value)
            Dim file : For Each file In fso.GetFolder(Expand(sourceDir)).Files
                If regex.Test(file.Name) Then
                    ListFile sourceDir, file.Name
                End If
            Next
        End Sub

        'Add a file to the dictionary object. Just the key is used, not the value.
        Sub ListFile(sourceDir, sourceFile)
            fileDict.Add format(Array("%s\%s", sourceDir, sourceFile)), ""
        End Sub

        Sub ExtractTheNextIcon
            Dim file 'filespec of the current file
            Dim base 'base name of the current file
            Dim ext 'extension name of the current file
            Dim largeTarget 'generated filespec for saving a large icon
            Dim smallTarget 'generated filespec for saving a small icon
            Dim suspect 'flespec of potentially-zero-size icon file
            Dim msg 'string: partial progress message
            If eStop Or fileIndex > UBound(fileKeys) Then
                ShowResults
                Exit Sub
            End If
            file = fileKeys(fileIndex)
            If iconIndex = 0 Then
                iconCount = extractor.IconCount(file)
            End If
            If (zeroIndexesOnlyChkBox.checked And iconIndex > 0) _
            Or iconIndex >= iconCount Then
                iconIndex = 0
                fileIndex = fileIndex + 1
                window.setTimeout "ExtractTheNextIcon", ms, "VBScript"
                Exit Sub
            End If
            base = fso.GetBaseName(file)
            ext = fso.GetExtensionName(file)
            largeTarget = format(Array( _
                "%s\lg\%s_%s_%s_Lg.ico", _
                targetFolder, base, ext, iconIndex))
            smallTarget = format(Array( _
                "%s\sm\%s_%s_%s_Sm.ico", _
                targetFolder, base, ext, iconIndex))
            If largeIconsChkBox.checked Then
                On Error Resume Next
                    extractor.Save file, iconIndex, largeTarget, True
                On Error Goto 0
            End If
            If smallIconsChkBox.checked Then
                On Error Resume Next
                    extractor.Save file, iconIndex, smallTarget, False
                On Error Goto 0
            End If
            For Each suspect In Array(largeTarget, smallTarget)
                RemoveIfZeroSize suspect
            Next
            If iconIndex = 0 Then msg = CheckingMsg Else msg = format(Array("Extracting icon %s from<br />", iconIndex))
            InProgressMsg msg & fileKeys(fileIndex)
            If fileIndex > UBound(fileKeys) Then
                ShowResults
                Exit Sub
            Else iconIndex = iconIndex + 1
            End If
            window.setTimeout "ExtractTheNextIcon", ms, "VBScript"
        End Sub

        Sub RemoveIfZeroSize(filespec)
            If Not fso.FileExists(filespec) Then Exit Sub
            If fso.GetFile(filespec).Size > 0 Then
                iconsExtracted = iconsExtracted + 1
                Exit Sub
            End If
            On Error Resume Next
                fso.DeleteFile filespec
            On Error Goto 0
        End Sub

        Sub sourceBrowserBtn_OnClick
            browser.Title = "Choose the icon source folder"
            browser.InitialDirectory = "%ProgramFiles%"
            Dim folder : folder = browser.FolderName
            If Not "" = folder Then sourceDirTxtBox.value = folder
        End Sub

        Sub targetBrowserBtn_OnClick
            browser.Title = "Create and/or choose a destination folder for the icons that will be extracted"
            browser.InitialDirectory = "%UserProfile%\Desktop"
            Dim folder : folder = browser.FolderName
            If Not "" = folder Then targetDirTxtBox.value = folder
        End Sub

        Sub stopBtn_OnClick
            eStop = True
            Disable stopBtn
        End Sub

        Sub elevateBtn_OnClick
            With CreateObject( "Shell.Application" )
                .ShellExecute "mshta", application.CommandLine,, "runas"
            End With
            Self.Close
        End Sub

        Sub Document_OnKeyUp
            If F6 = window.event.keyCode Then
                sh.Run format(Array("""%s"" %s", Expand(WordPad), application.commandLine))
            ElseIf F9 = window.event.keyCode Then
                sh.Run "notepad """ & configFile & """"
            ElseIf Enter = window.event.keyCode Then
                If eStop Then Exit Sub
                If extractIconsBtn.disabled Then Exit Sub
                window.setTimeout "extractIconsBtn_OnClick", ms, "VBScript"
            ElseIf Esc = window.event.keyCode Then
                Self.Close
            End If
        End Sub

        Sub smallIconsChkBox_OnClick : CheckIconChoices : End Sub
        Sub largeIconsChkBox_OnClick : CheckIconChoices : End Sub
        Sub CheckIconChoices
            If Not smallIconsChkBox.checked And Not largeIconsChkBox.checked Then Disable extractIconsBtn Else Enable extractIconsBtn
        End Sub

        Sub CreateFolder(newDir)
            Dim parentDir : parentDir = fso.GetParentFolderName(newDir)
            If Not fso.FolderExists(parentDir) Then CreateFolder parentDir
            If Not fso.FolderExists(newDir) Then fso.CreateFolder newDir
        End Sub

        Function Expand(str)
            Expand = sh.ExpandEnvironmentStrings(str)
        End Function

        Sub InProgressMsg(msg)
            inProgressDiv.innerHTML = msg
        End Sub

        Sub Feedback(msg)
            feedbackDiv.innerHTML = msg & "<br />" & feedbackDiv.innerHTML
            window.clearTimeout timeoutId
            timeoutId = window.setTimeout("ClearFeedback", 20000, "VBScript")
        End Sub

        Sub ClearFeedback : feedbackDiv.innerHTML = "" : End Sub

        Sub ReleaseObjects
            Set sh = Nothing
            Set fso = Nothing
            Set format = Nothing
            Set extractor = Nothing
            Set application = Nothing
            Set regex = Nothing
            Set includer = Nothing
            Set rf = Nothing
            Set log = Nothing
            Set browser = Nothing
            Set admin = Nothing
            Set fileDict = Nothing
        End Sub

        Sub Window_OnUnload
            ReleaseObjects
        End Sub
    </script>

    <p class="label"> Source folder
        <input
            type="checkbox"
            id="subfoldersChkBox"
            class="spaceLeft" /> and all subfolders
    </p>
    <input
        type="text"
        class="textInput"
        id="sourceDirTxtBox" />
    <input
        type="button"
        id="sourceBrowserBtn"
        class="narrowButton"
        title="Browse for source folder"
        value="..." /><br />
    <p class="label"> File name(s) </p>
    <input
        type="text"
        id="sourceFileTxtBox"
        class="textInput" /><br />
    <p class="label"> Target folder </p>
    <input
        type="text"
        id="targetDirTxtBox"
        class="textInput" />
    <input
        type="button"
        id="targetBrowserBtn"
        title="Browse for target folder"
        class="narrowButton"
        value="..." /><br />
    <input
        type="checkbox"
        id="largeIconsChkBox"
        class="spaceTop"
        checked /> <span class="label"> Large icons</span><br />
    <input
        type="checkbox"
        id="smallIconsChkBox"
                /> <span class="label"> Small icons</span><br />
    <input
        type="checkbox"
        id="zeroIndexesOnlyChkBox"
        checked /> <span class="label"> Zero indexes only</span><br />
    <input
        type="button"
        value="Extract icons"
        class="spaceTop"
        id="extractIconsBtn" />
    <input
        type="button"
        value="Stop"
        class="spaceLeft spaceTop"
        id="stopBtn" />
    <input
        type="button"
        value="Elevate"
        class="spaceLeft spaceTop"
        id="elevateBtn" /><br />
    <div
        id="inProgressDiv"
        class="spaceTop wide">
    </div>
    <div
        id="feedbackDiv"
        class="spaceTop" />
    </div>
    <style>
        body {
            font-family: sans-serif;
            background-color: buttonface;
            font-size: smaller;
            cursor: default;
        }
        .label {
            margin-bottom: 0px;
        }
        .textInput {
            width: 94%;
        }
        .spaceTop, .label {
            margin-top: 10px;
        }
        .narrowButton {
            padding: 0px;
        }
        #feedbackDiv {
            color: darkred;
        }
        .spaceLeft {
            margin-left: 10px;
        }
        .wide {
            width: 97%;
            border-width: 0px;
        }
    </style>
</body>
</html>